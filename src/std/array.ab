/// Returns index of the first value found in the specified array.
///
/// If the value is not found, the function returns -1.
pub fun array_find(array, value): Num {
    for index, element in array {
        if value as Text == element as Text {
            return index
        }
    }
    return -1
}

/// Searches for a value in an array and returns an array with the index of the various items.
pub fun array_find_all(array, value): [Num] {
    let result = [Num]
    for index, element in array {
        if value as Text == element as Text {
            result += [index]
        }
    }
    return result
}

/// Checks if a value is in the array.
pub fun array_contains(array, value) {
    let result = array_find(array, value)
    return result >= 0
}

/// Returns the first element in the array; if the array is empty, the return
/// value is undefined.
pub fun array_first(array) {
    return array[0]
}

/// Returns the last element in the array; if the array is empty, the return
/// value is undefined.
pub fun array_last(array) {
    let index = len(array) - 1
    return array[index]
}

/// Removes an element at the index from the array; if the index is negative
/// or beyond the end, the return value is undefined, but the array will be
/// unchanged.
pub fun array_remove_at(ref array: [], index: Num): Null {
    let offset = index + 1
    let length = len(array)
    array = array[0..index] + array[offset..length]
}

/// Removes an element at the index from the array, and returns it; if the
/// index is negative or beyond the end, the return value is undefined, but
/// the array will be unchanged.
pub fun array_extract_at(ref array, index) {
    let element = array[index]
    let offset = index + 1
    let length = len(array)
    array = array[0..index] + array[offset..length]
    return element
}

/// Removes the last element from the array, and returns it; if the array
/// is empty, the return value is undefined, but the array will be unchanged.
pub fun array_pop(ref array) {
    let length = len(array)
    let index = length - 1
    let element = array[index]
    array = array[0..index]
    return element
}

/// Removes the first element from the array, and returns it; if the array
/// is empty, the return value is undefined, but the array will be unchanged.
pub fun array_shift(ref array) {
    let length = len(array)
    let element = array[0]
    array = array[1..length]
    return element
}
