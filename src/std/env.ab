import * from "std/fs"
import * from "std/text"

pub fun get_env_var(var: Text, file: Text = ".env"): Text {
    let _var = unsafe $echo "\$\{!var}"$
    if _var != "" {
        return _var
    }

    if file_exist(".env") {
        unsafe $source "{file}"$
        return unsafe $echo "\$\{!var}"$
    }

    return ""
}

pub fun load_env_file(file: Text = ".env"): Null {
    unsafe $export "\$(xargs < {file})" > /dev/null$
}

pub fun shell_isset(name: Text): Bool {
    $[[ ! -z \$\{!{nameof name}+z} ]]$ failed {
        return false
    }
    return true
}

pub fun shell_constant_set(name: Text, val: Text): Null {
    $readonly \${nameof name}="\${nameof val}" 2> /dev/null$?
}

pub fun shell_constant_get(name: Text): Text {
    return $echo \$\{!{nameof name}}$?
}

pub fun shell_var_set(name: Text, val: Text): Null {
    $export \${nameof name}="\${nameof val}" 2> /dev/null$?
}

pub fun shell_var_get(name: Text): Text {
    return $echo \$\{!{nameof name}}$?
}

pub fun shell_unset(name: Text): Null {
    $unset {name}$?
}

pub fun is_command(command: Text): Bool {
    $[ -x "\$(command -v {command})" ]$ failed {
        return false
    }
    return true
}

pub fun input(prompt: Text): Text {
    unsafe $printf "\${nameof prompt}"$
    unsafe $read$
    return "\$REPLY"
}

// Confirm prompt (Yes/No), return true if choice is Yes
// "No" is the default choice, set default_yes to true for "Yes" as default choice
pub fun confirm(prompt: Text, default_yes: Bool = false): Bool {
    let choice_default = default_yes then " [\x1b[1mY/\x1b[0mn]" else " [y/\x1b[1mN\x1b[0m]"
    unsafe {
        $printf "\x1b[1m{prompt}\x1b[0m{choice_default}"$
        $read -s -n 1$
        $printf "\n"$
    }
    let result = lower(unsafe $echo \$REPLY$)
    return result == "y" or (result == "" and default_yes)
}

pub fun has_failed(command: Text): Bool {
    unsafe silent $eval {command}$
    return status != 0
}

pub fun exit(code: Num): Null {
    unsafe $exit "{code}"$
}

pub fun is_root(): Bool {
    if unsafe $id -u$ == "0" {
        return true
    }

    return false
}

pub fun printf(format: Text, args: [Text] = [""]): Null {
    unsafe ${nameof args}=("{format}" "\$\{{nameof args}[@]}")$
    unsafe $printf "\$\{{nameof args}[@]}"$
}

pub fun printf_escape(text: Text): Text {
    return unsafe $echo \${nameof text} | sed -e 's/\\\\/\\\\\\\\/g' -e "s/%/%%/g"$
}

pub fun text_shell(message: Text, style: Num, fg: Num, bg: Num): Text {
    return "\x1b[{style};{fg};{bg}m{printf_escape(message)}\x1b[0m"
}

pub fun text_bold(message: Text): Text {
    return "\x1b[1m{printf_escape(message)}\x1b[0m"
}

pub fun text_italic(message: Text): Text {
    return "\x1b[3m{printf_escape(message)}\x1b[0m"
}

pub fun text_underlined(message: Text): Text {
    return "\x1b[4m{printf_escape(message)}\x1b[0m"
}

pub fun color_echo(message: Text, color: Num): Null {
    printf("\x1b[{color as Text}m%s\x1b[0m\n", [message])
}

pub fun echo_info(message: Text): Null {
    printf("\x1b[1;3;97;44m %s \x1b[0m\n", [message])
}

pub fun echo_success(message: Text): Null {
    printf("\x1b[1;3;97;42m %s \x1b[0m\n", [message])
}

pub fun echo_warning(message: Text): Null {
    printf("\x1b[1;3;97;43m %s \x1b[0m\n", [message])
}

pub fun error(message: Text, exit_code: Num = 1): Null {
    printf("\x1b[1;3;97;41m %s \x1b[0m\n", [message])
    if exit_code > 0 : exit(exit_code)
}




