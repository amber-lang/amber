import * from "std/fs"
import * from "std/text"

/// Retrieves the value of an environment variable, optionally sourcing it from a file if not already set.
///
/// ### Usage
/// ```ab
/// let value = env_var_load("MY_VAR", ".env.local")
/// ```
pub fun env_var_load(var: Text, file: Text = ".env"): Text {
    const _var = trust $ echo "\$\{!{nameof var}}" $
    if _var != "" {
        return _var
    }

    if file_exists(file) {
        trust $ source "{file}" $
        return trust $ echo "\$\{!{nameof var}}" $
    }

    return ""
}

/// Loads the env file in the environment, using `xargs`.
///
/// ### Usage
/// ```ab
/// env_file_load(".env")
/// ```
pub fun env_file_load(file: Text = ".env"): Null {
    trust $ export "\$(xargs < {file})" > /dev/null $
}

/// Checks if a variable inside the shell session exists.
///
/// ### Usage
/// ```ab
/// if env_var_test("PATH") {
///     echo "PATH exists"
/// }
/// ```
pub fun env_var_test(name: Text): Bool {
    $ [[ ! -z \$\{!{nameof name}+z} ]] $ failed {
        return false
    }
    return true
}

/// Sets a constant inside the shell session.
///
/// ### Usage
/// ```ab
/// env_const_set("API_KEY", "secret123")
/// ```
pub fun env_const_set(name: Text, val: Text): Null? {
    $ readonly \${nameof name}="\${nameof val}" 2> /dev/null $?
}

/// Sets a variable inside the shell session.
///
/// ### Usage
/// ```ab
/// env_var_set("DEBUG", "true")
/// ```
pub fun env_var_set(name: Text, val: Text): Null? {
    $ export \${nameof name}="\${nameof val}" 2> /dev/null $?
}

/// Gets a variable or constant inside the shell session.
///
/// ### Usage
/// ```ab
/// let debug = env_var_get("DEBUG")
/// ```
pub fun env_var_get(name: Text): Text? {
    return $ echo \$\{!{nameof name}} $?
}

/// Removes a variable inside the shell session.
///
/// ### Usage
/// ```ab
/// env_var_unset("TEMP_VAR")
/// ```
pub fun env_var_unset(name: Text): Null? {
    $ unset {name} $?
}

/// Checks if a command exists.
///
/// ### Usage
/// ```ab
/// if is_command("git") {
///     echo "Git is installed"
/// }
/// ```
pub fun is_command(command: Text): Bool {
    $ [ -x "\$(command -v "{command}")" ] $ failed {
        return false
    }
    return true
}

/// Creates a prompt and returns the value.
///
/// ### Usage
/// ```ab
/// let name = input_prompt("Enter your name: ")
/// ```
pub fun input_prompt(prompt: Text): Text {
    trust $ read -p "\${nameof prompt}" $
    return trust $ echo \$REPLY $
}

/// Creates a prompt, hides any user input and returns the value.
///
/// ### Usage
/// ```ab
/// let password = input_hidden("Enter password: ")
/// ```
pub fun input_hidden(prompt: Text): Text {
    trust {
        $ read -s -p "\${nameof prompt}" $
        $ echo "" >&2 $
        return $ echo \$REPLY $
    }
}

/// Creates a confirm prompt (Yes/No), and returns true if the choice is Yes.
///
/// "No" is the default choice, set default_yes to true for "Yes" as default choice.
///
/// ### Usage
/// ```ab
/// if input_confirm("Continue?", false) {
///     echo "Continuing..."
/// }
/// ```
pub fun input_confirm(prompt: Text, default_yes: Bool = false): Bool {
    const choice_default = default_yes then " [\x1b[1mY/\x1b[0mn]" else " [y/\x1b[1mN\x1b[0m]"
    trust {
        $ printf "\x1b[1m{prompt}\x1b[0m{choice_default}" $
        $ read -s -n 1 $
        $ printf "\n" $
    }
    const result = lowercase(trust $ echo \$REPLY $)
    return result == "y" or (result == "" and default_yes)
}

/// Checks if the command has failed.
///
/// ### Usage
/// ```ab
/// if has_failed("test -f config.txt") {
///     echo "File doesn't exist"
/// }
/// ```
pub fun has_failed(command: Text): Bool {
    trust silent $ eval {command} $
    return status != 0
}

/// Checks if the script is running with a user with root permission.
///
/// ### Usage
/// ```ab
/// if is_root() {
///     echo "Running as root"
/// }
/// ```
pub fun is_root(): Bool {
    if trust $ id -u $ == "0" {
        return true
    }

    return false
}

/// `printf` the text following the arguments.
///
/// ### Usage
/// ```ab
/// printf("Hello %s!", ["World"])
/// ```
pub fun printf(format: Text, args: [Text] = [""]): Null {
    trust $ {nameof args}=("{format}" "\$\{{nameof args}[@]}") $
    trust $ printf "\$\{{nameof args}[@]}" $
}

/// Escapes the text to be used with `printf`.
///
/// ### Usage
/// ```ab
/// printf("%s\n", [escaped("100% done\\n")])
/// ```
pub fun escaped(text: Text): Text {
    return trust $ echo \${nameof text} | sed -e 's/\\\\/\\\\\\\\/g' -e "s/%/%%/g" $
}

/// Prepares a text with formatting options for `printf`.
///
/// ### Usage
/// ```ab
/// printf("%s\n", [styled("Error!", 1, 31, 40)])
/// ```
pub fun styled(message: Text, style: Int, fg: Int, bg: Int): Text {
    return "\x1b[{style};{fg};{bg}m{escaped(message)}\x1b[0m"
}

/// Returns a text as bold.
///
/// ### Usage
/// ```ab
/// printf("%s\n", [bold("Important message")])
/// ```
pub fun bold(message: Text): Text {
    return "\x1b[1m{escaped(message)}\x1b[0m"
}

/// Returns a text as italic.
///
/// ### Usage
/// ```ab
/// printf("%s\n", [italic("Emphasized text")])
/// ```
pub fun italic(message: Text): Text {
    return "\x1b[3m{escaped(message)}\x1b[0m"
}

/// Returns a text as underlined.
///
/// ### Usage
/// ```ab
/// printf("%s\n", [underlined("Underlined text")])
/// ```
pub fun underlined(message: Text): Text {
    return "\x1b[4m{escaped(message)}\x1b[0m"
}

/// Prints a text with a specified color.
///
/// ### Usage
/// ```ab
/// echo_colored("Red text", 31)
/// ```
pub fun echo_colored(message: Text, color: Int): Null {
    printf("\x1b[{color as Text}m%s\x1b[0m\n", [message])
}

/// Prints a text as a info message.
///
/// ### Usage
/// ```ab
/// echo_info("Information message")
/// ```
pub fun echo_info(message: Text): Null {
    printf("\x1b[1;3;97;44m%s\x1b[0m\n", [message])
}

/// Prints a text as a success message.
///
/// ### Usage
/// ```ab
/// echo_success("Operation completed successfully")
/// ```
pub fun echo_success(message: Text): Null {
    printf("\x1b[1;3;97;42m%s\x1b[0m\n", [message])
}

/// Prints a text as a warning message.
///
/// ### Usage
/// ```ab
/// echo_warning("Warning: Disk space low")
/// ```
pub fun echo_warning(message: Text): Null {
    printf("\x1b[1;3;97;43m%s\x1b[0m\n", [message])
}

/// Prints a text as a error and exits if the status code is greater than 0.
///
/// ### Usage
/// ```ab
/// echo_error("Fatal error occurred", 1)
/// ```
pub fun echo_error(message: Text, exit_code: Int = 1): Null {
    printf("\x1b[1;3;97;41m%s\x1b[0m\n", [message])
    if exit_code > 0 : exit(exit_code)
}

/// Returns current bash version with major, minor and patch components.
///
/// ### Usage
/// ```ab
/// let version = bash_version()
/// echo "Bash {version[0]}.{version[1]}.{version[2]}"
/// ```
#[allow_absurd_cast]
pub fun bash_version(): [Int] {
    const major = trust $ echo "\$\{BASH_VERSINFO[0]}" $ as Int
    const minor = trust $ echo "\$\{BASH_VERSINFO[1]}" $ as Int
    const patch = trust $ echo "\$\{BASH_VERSINFO[2]}" $ as Int
    return [major, minor, patch]
}
